plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

group = "xyz.papermodloader"
version = getVersion()
archivesBaseName = "tree"
sourceCompatibility = targetCompatibility = "1.8"

if (System.getenv().BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven {
        name = "ilexiconn"
        url = "http://maven.ilexiconn.net"
    }
}

build.dependsOn(shadowJar)

dependencies {
    compile gradleApi()
    shade "com.google.code.gson:gson:2.7"
    shade "commons-io:commons-io:2.5"
    shade "org.ow2.asm:asm-debug-all:5.1"
    shade "org.apache.commons:commons-lang3:3.4"
    shade "xyz.papermodloader:book:0.1.0.12"
    shade "com.google.guava:guava:19.0"
}

sourceSets {
    main.compileClasspath += configurations.shade;
    main.runtimeClasspath += configurations.shade;
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shade]
}

String getVersion() {
    String version = "0.0.0"
    String prefix = "public static final String VERSION = \"";
    File file = file("src/main/java/xyz/papermodloader/tree/Tree.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            version = s;
        }
    }
    return version;
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "xyz.papermodloader"
            artifactId "tree"
            from components.shadow
        }
    }
    repositories {
        maven {
            credentials {
                if (System.getenv().PASSWORD) {
                    username System.getenv().USERNAME
                    password System.getenv().PASSWORD
                } else {
                    username ""
                    password ""
                }
            }
            if (System.getenv().PASSWORD) {
                url "sftp://connect.ilexiconn.net:22/home/ilexiconn/maven/"
            } else {
                url "file:///var/www/maven/"
            }
        }
    }
}
