buildscript {
    repositories {
        jcenter();
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

group = "xyz.papermodloader"
version = getVersion()
archivesBaseName = "tree"
sourceCompatibility = targetCompatibility = "1.8"

if (System.getenv().BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven {
        name = "cuchaz"
        url = "http://maven.cuchazinteractive.com"
    }
}

build.dependsOn(shadowJar)

dependencies {
    compile gradleApi()
    shade "com.google.code.gson:gson:2.7"
    shade "commons-io:commons-io:2.5"
    shade "cuchaz:enigma-lib:0.11b"
    shade "org.ow2.asm:asm-debug-all:5.1"
    shade "org.apache.commons:commons-lang3:3.4"
}

sourceSets {
    main.compileClasspath += configurations.shade;
    main.runtimeClasspath += configurations.shade;
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shade]
}

String getVersion() {
    String version = "0.0.0"
    String prefix = "public static final String VERSION = \"";
    File file = file("src/main/java/xyz/papermodloader/tree/Tree.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            version = s;
        }
    }
    return version;
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "xyz.papermodloader"
            artifactId "tree"
            from components.shadow
        }
    }
    repositories {
        maven {
            credentials {
                if (System.getenv().Passowrd) {
                    username System.getenv().Name
                    password System.getenv().Passowrd
                } else {
                    username ""
                    password ""
                }
            }
            if (System.getenv().Passowrd) {
                url System.getenv().Repo
            } else {
                url "file:///var/www/maven/"
            }
        }
    }
}
